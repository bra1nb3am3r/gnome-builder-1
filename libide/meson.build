libide_enum_headers = [
  'buffers/ide-buffer.h',
  'buildsystem/ide-build-result.h',
  'devices/ide-device.h',
  'diagnostics/ide-diagnostic.h',
  'doap/ide-doap.h',
  'files/ide-indent-style.h',
  'highlighting/ide-highlighter.h',
  'runtimes/ide-runtime.h',
  'sourceview/ide-source-view.h',
  'symbols/ide-symbol.h',
  'threading/ide-thread-pool.h',
  'vcs/ide-vcs-config.h',
  'workbench/ide-layout-stack-split.h',
]

libide_header_subdir = 'gnome-builder-' + meson.project_version() + '/libide'
libide_header_dir = path_join(get_option('includedir'), libide_header_subdir)

# https://github.com/mesonbuild/meson/pull/718
libide_enums = gnome.mkenums('ide-enums',
  h_template: 'ide-enums.h.in',
  c_template: 'ide-enums.c.in',
  sources: libide_enum_headers,
  install_header: true,
  install_dir: libide_header_dir,
)

libide_public_headers = [
  'ide-context.h',
  'ide-global.h',
  'ide-macros.h',
  'ide-object.h',
  'ide-service.h',
  'ide-types.h',
  'ide.h',
  'application/ide-application-addin.h',
  'application/ide-application-credits.h',
  'application/ide-application-tool.h',
  'application/ide-application.h',
  'buffers/ide-buffer-change-monitor.h',
  'buffers/ide-buffer-manager.h',
  'buffers/ide-buffer.h',
  'buffers/ide-unsaved-file.h',
  'buffers/ide-unsaved-files.h',
  'buildsystem/ide-build-command.h',
  'buildsystem/ide-build-command-queue.h',
  'buildsystem/ide-build-manager.h',
  'buildsystem/ide-build-result-addin.h',
  'buildsystem/ide-build-result.h',
  'buildsystem/ide-build-system.h',
  'buildsystem/ide-build-target.h',
  'buildsystem/ide-builder.h',
  'buildsystem/ide-configuration-manager.h',
  'buildsystem/ide-configuration.h',
  'buildsystem/ide-environment-variable.h',
  'buildsystem/ide-environment.h',
  'devices/ide-device-manager.h',
  'devices/ide-device-provider.h',
  'devices/ide-device.h',
  'diagnostics/ide-diagnostic-provider.h',
  'diagnostics/ide-diagnostic.h',
  'diagnostics/ide-diagnostician.h',
  'diagnostics/ide-diagnostics.h',
  'diagnostics/ide-fixit.h',
  'diagnostics/ide-source-location.h',
  'diagnostics/ide-source-range.h',
  'directory/ide-directory-build-system.h',
  'directory/ide-directory-vcs.h',
  'doap/ide-doap-person.h',
  'doap/ide-doap.h',
  'editor/ide-editor-perspective.h',
  'editor/ide-editor-view-addin.h',
  'editor/ide-editor-view.h',
  'files/ide-file-settings.h',
  'files/ide-file.h',
  'files/ide-indent-style.h',
  'genesis/ide-genesis-addin.h',
  'highlighting/ide-highlight-engine.h',
  'highlighting/ide-highlight-index.h',
  'highlighting/ide-highlighter.h',
  'history/ide-back-forward-item.h',
  'history/ide-back-forward-list.h',
  'local/ide-local-device.h',
  'logging/ide-log.h',
  'plugins/ide-extension-adapter.h',
  'plugins/ide-extension-set-adapter.h',
  'preferences/ide-preferences-addin.h',
  'preferences/ide-preferences-bin.h',
  'preferences/ide-preferences-entry.h',
  'preferences/ide-preferences-file-chooser-button.h',
  'preferences/ide-preferences-flow-box.h',
  'preferences/ide-preferences-font-button.h',
  'preferences/ide-preferences-group.h',
  'preferences/ide-preferences-page.h',
  'preferences/ide-preferences-spin-button.h',
  'preferences/ide-preferences-switch.h',
  'preferences/ide-preferences.h',
  'projects/ide-project-file.h',
  'projects/ide-project-files.h',
  'projects/ide-project-info.h',
  'projects/ide-project-item.h',
  'projects/ide-project-miner.h',
  'projects/ide-project.h',
  'projects/ide-recent-projects.h',
  'runner/ide-run-button.h',
  'runner/ide-run-manager.h',
  'runner/ide-runner.h',
  'runner/ide-runner-addin.h',
  'runtimes/ide-runtime-manager.h',
  'runtimes/ide-runtime-provider.h',
  'runtimes/ide-runtime.h',
  'scripting/ide-script-manager.h',
  'scripting/ide-script.h',
  'search/ide-omni-search-display.h',
  'search/ide-omni-search-entry.h',
  'search/ide-omni-search-group.h',
  'search/ide-omni-search-row.h',
  'search/ide-pattern-spec.h',
  'search/ide-search-context.h',
  'search/ide-search-engine.h',
  'search/ide-search-provider.h',
  'search/ide-search-reducer.h',
  'search/ide-search-result.h',
  'snippets/ide-source-snippet-chunk.h',
  'snippets/ide-source-snippet-context.h',
  'snippets/ide-source-snippet.h',
  'snippets/ide-source-snippets-manager.h',
  'snippets/ide-source-snippets.h',
  'sourceview/ide-completion-item.h',
  'sourceview/ide-completion-provider.h',
  'sourceview/ide-completion-results.h',
  'sourceview/ide-indenter.h',
  'sourceview/ide-language.h',
  'sourceview/ide-source-map.h',
  'sourceview/ide-source-style-scheme.h',
  'sourceview/ide-source-view-mode.h',
  'sourceview/ide-source-view.h',
  'subprocess/ide-subprocess.h',
  'subprocess/ide-subprocess-launcher.h',
  'symbols/ide-symbol-node.h',
  'symbols/ide-symbol-resolver.h',
  'symbols/ide-symbol-tree.h',
  'symbols/ide-symbol.h',
  'symbols/ide-tags-builder.h',
  'template/ide-project-template.h',
  'template/ide-template-base.h',
  'template/ide-template-provider.h',
  'threading/ide-thread-pool.h',
  'transfers/ide-transfer.h',
  'transfers/ide-transfer-manager.h',
  'transfers/ide-transfer-row.h',
  'transfers/ide-transfers-button.h',
  'transfers/ide-transfers-progress-icon.h',
  'tree/ide-tree-builder.h',
  'tree/ide-tree-node.h',
  'tree/ide-tree-types.h',
  'tree/ide-tree.h',
  'util/ide-cairo.h',
  'util/ide-dnd.h',
  'util/ide-file-manager.h',
  'util/ide-flatpak.h',
  'util/ide-glib.h',
  'util/ide-gtk.h',
  'util/ide-line-reader.h',
  'util/ide-list-inline.h',
  'util/ide-pango.h',
  'util/ide-posix.h',
  'util/ide-progress.h',
  'util/ide-rgba.h',
  'util/ide-settings.h',
  'util/ide-uri.h',
  'vcs/ide-vcs-config.h',
  'vcs/ide-vcs-initializer.h',
  'vcs/ide-vcs-uri.h',
  'vcs/ide-vcs.h',
  'workbench/ide-layout-grid.h',
  'workbench/ide-layout-pane.h',
  'workbench/ide-layout-stack-split.h',
  'workbench/ide-layout-stack.h',
  'workbench/ide-layout-stack-addin.h',
  'workbench/ide-layout-view.h',
  'workbench/ide-layout.h',
  'workbench/ide-omni-bar.h',
  'workbench/ide-perspective.h',
  'workbench/ide-workbench-addin.h',
  'workbench/ide-workbench-header-bar.h',
  'workbench/ide-workbench.h',
  'workers/ide-worker.h',
]

libide_conf = configuration_data()
libide_conf.set10('ENABLE_TRACING', get_option('enable_tracing'))
# FIXME: BUGREPORT_URL
libide_debug_h = configure_file(
  input: 'ide-debug.h.in',
  output: 'ide-debug.h',
  configuration: libide_conf,
  install: true,
  install_dir: libide_header_dir,
)


install_headers(libide_public_headers,
  subdir: libide_header_subdir,
)

subdir('resources')

libide_icons_resources = gnome.compile_resources('ide-icons-resources',
  path_join(meson.source_root(), 'data/icons/hicolor/icons.gresource.xml'),
  source_dir: path_join(meson.source_root(), 'data/icons/hicolor'),
  c_name: 'ide_icons',
)

libide_public_sources = [
  'application/ide-application-addin.c',
  'application/ide-application-tool.c',
  'application/ide-application.c',
  'application/ide-application-open.c',
  'buffers/ide-buffer-change-monitor.c',
  'buffers/ide-buffer-manager.c',
  'buffers/ide-buffer.c',
  'buffers/ide-unsaved-file.c',
  'buffers/ide-unsaved-files.c',
  'buildsystem/ide-build-command.c',
  'buildsystem/ide-build-command-queue.c',
  'buildsystem/ide-build-manager.c',
  'buildsystem/ide-build-result-addin.c',
  'buildsystem/ide-build-result.c',
  'buildsystem/ide-build-system.c',
  'buildsystem/ide-build-target.c',
  'buildsystem/ide-builder.c',
  'buildsystem/ide-configuration-manager.c',
  'buildsystem/ide-configuration.c',
  'buildsystem/ide-environment-variable.c',
  'buildsystem/ide-environment.c',
  'devices/ide-device-manager.c',
  'devices/ide-device-provider.c',
  'devices/ide-device.c',
  'diagnostics/ide-diagnostic-provider.c',
  'diagnostics/ide-diagnostic.c',
  'diagnostics/ide-diagnostician.c',
  'diagnostics/ide-diagnostics.c',
  'diagnostics/ide-fixit.c',
  'diagnostics/ide-source-location.c',
  'diagnostics/ide-source-range.c',
  'directory/ide-directory-build-system.c',
  'directory/ide-directory-plugin.c',
  'directory/ide-directory-vcs.c',
  'doap/ide-doap-person.c',
  'doap/ide-doap.c',
  'editor/ide-editor-perspective.c',
  'editor/ide-editor-view-addin.c',
  'editor/ide-editor-view.c',
  'files/ide-file-settings.c',
  'files/ide-file.c',
  'genesis/ide-genesis-addin.c',
  'highlighting/ide-highlight-engine.c',
  'highlighting/ide-highlight-index.c',
  'highlighting/ide-highlighter.c',
  'history/ide-back-forward-item.c',
  'history/ide-back-forward-list-load.c',
  'history/ide-back-forward-list-save.c',
  'history/ide-back-forward-list.c',
  'ide-context.c',
  libide_enums[0],
  'ide-object.c',
  'ide-service.c',
  'ide.c',
  'local/ide-local-device.c',
  'logging/ide-log.c',
  'plugins/ide-extension-adapter.c',
  'plugins/ide-extension-set-adapter.c',
  'preferences/ide-preferences-addin.c',
  'preferences/ide-preferences-entry.c',
  'preferences/ide-preferences-file-chooser-button.c',
  'preferences/ide-preferences-flow-box.c',
  'preferences/ide-preferences-font-button.c',
  'preferences/ide-preferences-group.c',
  'preferences/ide-preferences-page.c',
  'preferences/ide-preferences-spin-button.c',
  'preferences/ide-preferences-switch.c',
  'preferences/ide-preferences.c',
  'projects/ide-project-file.c',
  'projects/ide-project-files.c',
  'projects/ide-project-info.c',
  'projects/ide-project-item.c',
  'projects/ide-project-miner.c',
  'projects/ide-project.c',
  'projects/ide-recent-projects.c',
  'runner/ide-run-button.c',
  'runner/ide-run-manager.c',
  'runner/ide-runner.c',
  'runner/ide-runner-addin.c',
  'runtimes/ide-runtime-manager.c',
  'runtimes/ide-runtime-provider.c',
  'runtimes/ide-runtime.c',
  'scripting/ide-script-manager.c',
  'scripting/ide-script.c',
  'search/ide-omni-search-display.c',
  'search/ide-omni-search-entry.c',
  'search/ide-omni-search-group.c',
  'search/ide-omni-search-row.c',
  'search/ide-pattern-spec.c',
  'search/ide-search-context.c',
  'search/ide-search-engine.c',
  'search/ide-search-provider.c',
  'search/ide-search-result.c',
  'snippets/ide-source-snippet-chunk.c',
  'snippets/ide-source-snippet-context.c',
  'snippets/ide-source-snippet.c',
  'snippets/ide-source-snippets-manager.c',
  'snippets/ide-source-snippets.c',
  'sourceview/ide-completion-item.c',
  'sourceview/ide-completion-provider.c',
  'sourceview/ide-completion-results.c',
  'sourceview/ide-indenter.c',
  'sourceview/ide-language.c',
  'sourceview/ide-source-map.c',
  'sourceview/ide-source-style-scheme.c',
  'sourceview/ide-source-view-mode.c',
  'sourceview/ide-source-view.c',
  'subprocess/ide-subprocess.c',
  'subprocess/ide-subprocess-launcher.c',
  'symbols/ide-symbol-node.c',
  'symbols/ide-symbol-resolver.c',
  'symbols/ide-symbol-tree.c',
  'symbols/ide-symbol.c',
  'symbols/ide-tags-builder.c',
  'template/ide-project-template.c',
  'template/ide-template-base.c',
  'template/ide-template-provider.c',
  'threading/ide-thread-pool.c',
  'transfers/ide-transfer.c',
  'transfers/ide-transfer-manager.c',
  'transfers/ide-transfer-row.c',
  'transfers/ide-transfers-button.c',
  'transfers/ide-transfers-progress-icon.c',
  'tree/ide-tree-builder.c',
  'tree/ide-tree-node.c',
  'tree/ide-tree.c',
  'util/ide-cairo.c',
  'util/ide-dnd.c',
  'util/ide-file-manager.c',
  'util/ide-flatpak.c',
  'util/ide-glib.c',
  'util/ide-gtk.c',
  'util/ide-line-reader.c',
  'util/ide-pango.c',
  'util/ide-posix.c',
  'util/ide-progress.c',
  'util/ide-rgba.c',
  'util/ide-settings.c',
  'util/ide-uri.c',
  'vcs/ide-vcs-config.c',
  'vcs/ide-vcs-initializer.c',
  'vcs/ide-vcs-uri.c',
  'vcs/ide-vcs.c',
  'workbench/ide-layout-grid.c',
  'workbench/ide-layout-pane.c',
  'workbench/ide-layout-stack.c',
  'workbench/ide-layout-stack-addin.c',
  'workbench/ide-layout-view.c',
  'workbench/ide-layout.c',
  'workbench/ide-omni-bar.c',
  'workbench/ide-perspective.c',
  'workbench/ide-workbench-addin.c',
  'workbench/ide-workbench-header-bar.c',
  'workbench/ide-workbench-open.c',
  'workbench/ide-workbench.c',
  'workers/ide-worker.c',
]

libide_generated_headers = [
  libide_resources[1],
  libide_icons_resources[1],
  libide_enums[1],
  libide_debug_h,
]

libide_sources = libide_generated_headers + libide_public_sources + [
  libide_resources[0],
  libide_icons_resources[0],
  'application/ide-application-actions.c',
  'application/ide-application-actions.h',
  'application/ide-application-command-line.c',
  'application/ide-application-plugins.c',
  'application/ide-application-private.h',
  'application/ide-application-tests.c',
  'application/ide-application-tests.h',
  'editor/ide-editor-frame-actions.c',
  'editor/ide-editor-frame-actions.h',
  'editor/ide-editor-frame-private.h',
  'editor/ide-editor-frame.c',
  'editor/ide-editor-frame.h',
  'editor/ide-editor-map-bin.c',
  'editor/ide-editor-map-bin.h',
  'editor/ide-editor-plugin.c',
  'editor/ide-editor-layout-stack-addin.c',
  'editor/ide-editor-layout-stack-addin.h',
  'editor/ide-editor-layout-stack-controls.c',
  'editor/ide-editor-layout-stack-controls.h',
  'editor/ide-editor-print-operation.c',
  'editor/ide-editor-print-operation.h',
  'editor/ide-editor-tweak-widget.c',
  'editor/ide-editor-tweak-widget.h',
  'editor/ide-editor-view-actions.c',
  'editor/ide-editor-view-actions.h',
  'editor/ide-editor-view-addin-private.h',
  'editor/ide-editor-view-private.h',
  'editor/ide-editor-workbench-addin.c',
  'editor/ide-editor-workbench-addin.h',
  'gconstructor.h',
  'greeter/ide-greeter-perspective.c',
  'greeter/ide-greeter-perspective.h',
  'greeter/ide-greeter-project-row.c',
  'greeter/ide-greeter-project-row.h',
  'gsettings/ide-gsettings-file-settings.c',
  'gsettings/ide-gsettings-file-settings.h',
  'gsettings/ide-language-defaults.c',
  'gsettings/ide-language-defaults.h',
  'history/ide-back-forward-list-private.h',
  'ide-internal.h',
  'keybindings/ide-keybindings.c',
  'keybindings/ide-keybindings.h',
  'keybindings/ide-shortcuts-window.c',
  'keybindings/ide-shortcuts-window.h',
  'modelines/ide-modelines-file-settings.c',
  'modelines/ide-modelines-file-settings.h',
  'modelines/modeline-parser.c',
  'modelines/modeline-parser.h',
  'plugins/ide-extension-util.c',
  'plugins/ide-extension-util.h',
  'preferences/ide-preferences-bin-private.h',
  'preferences/ide-preferences-bin.c',
  'preferences/ide-preferences-builtin.c',
  'preferences/ide-preferences-builtin.h',
  'preferences/ide-preferences-group-private.h',
  'preferences/ide-preferences-language-row.c',
  'preferences/ide-preferences-language-row.h',
  'preferences/ide-preferences-page-private.h',
  'preferences/ide-preferences-perspective.c',
  'preferences/ide-preferences-perspective.h',
  'runner/ide-run-manager-private.h',
  'search/ide-search-reducer.c',
  'snippets/ide-source-snippet-completion-item.c',
  'snippets/ide-source-snippet-completion-item.h',
  'snippets/ide-source-snippet-completion-provider.c',
  'snippets/ide-source-snippet-completion-provider.h',
  'snippets/ide-source-snippet-parser.c',
  'snippets/ide-source-snippet-parser.h',
  'snippets/ide-source-snippet-private.h',
  'sourceview/ide-line-change-gutter-renderer.c',
  'sourceview/ide-line-change-gutter-renderer.h',
  'sourceview/ide-line-diagnostics-gutter-renderer.c',
  'sourceview/ide-line-diagnostics-gutter-renderer.h',
  'sourceview/ide-source-iter.c',
  'sourceview/ide-source-iter.h',
  'sourceview/ide-source-view-capture.c',
  'sourceview/ide-source-view-capture.h',
  'sourceview/ide-source-view-movements.c',
  'sourceview/ide-source-view-movements.h',
  'sourceview/ide-text-iter.c',
  'sourceview/ide-text-iter.h',
  'sourceview/ide-text-util.c',
  'sourceview/ide-text-util.h',
  'subprocess/ide-breakout-subprocess.c',
  'subprocess/ide-breakout-subprocess.h',
  'subprocess/ide-breakout-subprocess-private.h',
  'subprocess/ide-simple-subprocess.c',
  'subprocess/ide-simple-subprocess.h',
  'theatrics/ide-box-theatric.c',
  'theatrics/ide-box-theatric.h',
  'theming/ide-css-provider.c',
  'theming/ide-css-provider.h',
  'theming/ide-theme-manager.c',
  'theming/ide-theme-manager.h',
  'tree/ide-tree-private.h',
  'util/ide-async-helper.c',
  'util/ide-async-helper.h',
  'util/ide-battery-monitor.c',
  'util/ide-battery-monitor.h',
  'util/ide-doc-seq.c',
  'util/ide-doc-seq.h',
  'util/ide-gdk.c',
  'util/ide-gdk.h',
  'util/ide-ref-ptr.c',
  'util/ide-ref-ptr.h',
  'util/ide-window-settings.c',
  'util/ide-window-settings.h',
  'workbench/ide-layout-stack-actions.c',
  'workbench/ide-layout-stack-actions.h',
  'workbench/ide-layout-stack-private.h',
  'workbench/ide-layout-tab-bar.c',
  'workbench/ide-layout-tab-bar.h',
  'workbench/ide-layout-tab-bar-private.h',
  'workbench/ide-layout-tab.c',
  'workbench/ide-layout-tab.h',
  'workbench/ide-layout-tab-private.h',
  'workbench/ide-omni-bar-row.c',
  'workbench/ide-omni-bar-row.h',
  'workbench/ide-perspective-menu-button.c',
  'workbench/ide-perspective-menu-button.h',
  'workbench/ide-workbench-actions.c',
  'workbench/ide-workbench-private.h',
  'workers/ide-worker-manager.c',
  'workers/ide-worker-manager.h',
  'workers/ide-worker-process.c',
  'workers/ide-worker-process.h',
]

libide_deps = [
  dependency('gio-2.0 >= 2.49.0'),
  dependency('gio-unix-2.0 >= 2.49.0'),
  dependency('gtk+-3.0 >= 3.21.5'),
  dependency('gtksourceview-3.0 >= 3.21.2'),
  dependency('libpeas-1.0 >= 1.18.0'),
  dependency('libxml-2.0 >= 2.9.0'),
  dependency('pangoft2 >= 1.38.0'),
  libegg_dep,
  libpnl_dep,
  libtmpl_dep,
]

contrib_dir = path_join(meson.source_root(), 'contrib/')

libide_includes = [
  contrib_dir + 'gd',
  contrib_dir + 'nautilus',
  contrib_dir + 'search',
  contrib_dir + 'xml',
]

libide_libs = [
  libgd,
  libnautilus,
  libsearch,
  libxml,
]

if get_option('with_webkit')
  libide_sources += ['webkit/ide-webkit.c']
  libide_deps += [dependency('webkit2gtk-4.0 >= 2.12.0')]
endif

libide_args = []
if get_option('with_editorconfig')
  libide_args += '-DENABLE_EDITORCONFIG'
  libide_libs += libeditorconfig
  libide_includes += [
    contrib_dir + 'libeditorconfig',
    meson.current_source_dir() + '/editorconfig',
  ]
  libide_sources += [
    'editorconfig/editorconfig-glib.c',
    'editorconfig/ide-editorconfig-file-settings.c',
  ]
endif

libide_api_version = '1.0'

libide = shared_library('ide-' + libide_api_version, libide_sources,
  dependencies: libide_deps,
  include_directories: include_directories(libide_includes),
  link_with: libide_libs,
  c_args: libide_args,
  install: true,
  install_dir: pkglibdir,
)

libide_dep = declare_dependency(
  sources: libide_generated_headers,
  dependencies: [
    dependency('gio-2.0 >= 2.49.0'),
    dependency('gtk+-3.0 >= 3.21.5'),
    dependency('gtksourceview-3.0 >= 3.21.2'),
    libpnl_dep,
    libegg_dep,
  ],
  link_with: libide,
  include_directories: include_directories([
    meson.current_source_dir(),
    meson.current_build_dir(),
  ])
)

if get_option('with_introspection')

gnome.generate_gir(libide,
  sources: libide_generated_headers + libide_public_headers + libide_public_sources,
  nsversion: libide_api_version,
  namespace: 'Ide',
  symbol_prefix: 'ide',
  identifier_prefix: 'Ide',
  #dependencies: libide_dep,
  includes: ['Gio-2.0', 'Gtk-3.0', 'GtkSource-3.0', 'Peas-1.0', libtmpl_gir[0]],
  install: true,
  install_dir_gir: pkggirdir,
  install_dir_typelib: pkgtypelibdir,
  extra_args: [
    '-I' + meson.current_build_dir(),
  ]
)

endif
